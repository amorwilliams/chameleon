apply plugin: 'android'

// loading the productFlavor.json
import groovy.json.JsonSlurper
def loadProductFlavors() {
    try {
        def slurper = new JsonSlurper()
        File f = new File("${projectDir}/productflavor.json")
        def result = slurper.parseText(f.text)
        return result
    } catch(e) {
        print e
        return []
    }
}

def platformFlavors = loadProductFlavors()

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.1'
    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 18
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    flavorGroups "chameleon"
    // generate productFlavors according to the setting of productFlavor.json
    productFlavors {
        platformFlavors.each { x ->
            "$x.productFlavor" {
                x.cfg.each { item, value ->
                    "$item" "$value"
                }
                flavorGroup "chameleon"
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:+'
    compile files("libs/chameleon_sdk.jar")
    // generate the compile dependency
    platformFlavors.each {x ->
        "${x.productFlavor}Compile" fileTree(dir: "${projectDir}/src/${x.productFlavor}/libs", includes: ['*.jar'])
    }
}



